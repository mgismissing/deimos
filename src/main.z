use * from cwio
use * from cwio.const
use Font from cwio.font
use utime
use machine
use Pin from machine
use sys
use os
use ui
use choose from ui
use res
use apps

class DebugInterrupt from Exception;;.

=== debug sleep
utime.sleep(2)

init!

screen.on!
screen.init!
keyboard.init!

scr = ui.Screen!

scr \
    + (title    := ui.Label(0, 0, "Welcome to Deimos", SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)) \
    + (desc     := ui.Label(0, 14, \
        "This OS is built on PicoEASE, a library that's\n"+\
        "used to access the calculator's debug interface.\n"+\
        "This allows the pico to access internal memory\n"+\
        "addresses that are mapped to external devices\n"+\
        "like the screen and the keyboard.", \
    SCR.ENUM.PIXEL.BLACK, font.miniwi))

applist = [=]
for app -> apps.all
    applist[app.name] = app
.

try
    while True
        screen.clear!
        scr.render!
        screen.apply!
        key = keyboard.get_next!
        if key == KEYBOARD.KEYS.SETTINGS
            c = choose(["Power", "Debug"])
            if c == 0
                c = choose(["Restart", "Reboot"])
                if c == 0
                    machine.soft_reset!
                .elif c == 1
                    machine.reset!
                .
            .elif c == 1
                screen.clear!
                screen.write("Debug mode", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                screen.write("Control has been redirected to USB", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                screen.apply!
                raise DebugInterrupt("Returning to REPL")
            .
        .elif key == KEYBOARD.KEYS.HOME
            c = choose(applist)
            if c >= 0
                app = apps.all[c]
                try
                    app.main!
                .catch Exception = e
                    screen.clear!
                    screen.write("Tripped and fell", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                    screen.write(e?.__name__ .. ":\n" .. e as str, 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                    screen.apply!
                    raise e
                .
            .
        .
    .
.catch DebugInterrupt = e
    <|"DEBUG: "..(e as str)
.