use * from cwio
use * from cwio.const
use Font from cwio.font
use utime
use machine
use Pin from machine
use sys
use os
use ui
use choose from ui
use res
use apps

class DebugInterrupt from Exception;;.

init!

screen.on!
screen.init!
keyboard.init!

scr = ui.Screen!

scr \
    + (title    := ui.Label(0, 0, "Welcome to Deimos", SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)) \
    + (desc     := ui.Label(0, 20, \
        "Press HOME for a list of apps.\n"+\
        "\n"+\
        "Go to https://github.com/mgismissing/deimos for\n"+\
        "more info about this project.", \
    SCR.ENUM.PIXEL.BLACK, font.miniwi))

applist = [,]
for app -> apps.all
    {applist}<-("[LIB] " if hasattr(app, "lib") else "")..app.name
.

def run_app(app)
    try
        app.main!
    .catch Exception = e
        screen.clear!
        screen.write(e?.__name__, 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
        screen.write(e as str, 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
        screen.apply!
        raise e
    .
.

try
    while True
        screen.clear!
        scr.render!
        screen.apply!
        key = keyboard.get_next!
        if key == KB.KEYS.SETTINGS
            c = choose(["Power", "Debug"])
            if c == 0
                c = choose(["Restart", "Reboot"])
                if c == 0
                    machine.soft_reset!
                .elif c == 1
                    machine.reset!
                .
            .elif c == 1
                screen.clear!
                screen.write("Debug mode", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                screen.write("Control has been redirected to USB", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                screen.apply!
                raise DebugInterrupt("Returning to REPL")
            .
        .elif key == KB.KEYS.HOME
            c = choose(applist)
            if c >= 0
                run_app(apps.all[c])
            .
        .elif key == KB.KEYS.TOOLS
            ui.choose_file!
        .
    .
.catch DebugInterrupt = e
    <|"DEBUG: "..(e as str)
.