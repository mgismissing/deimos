use * from cwio
use * from cwio.const
use Font from cwio.font
use time
use machine
use Pin from machine
use sys
use os
use ui
use choose from ui
use res
use wifi
use wlan from wifi
use update
use garbage
use loader
use apps from loader

class DebugInterrupt from Exception;;.

init!

screen.on!
screen.init!
keyboard.init!

scr = ui.Screen!

scr \
    + (title    := ui.Label(0, 0, "Welcome to Deimos", SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)) \
    + (desc     := ui.Label(0, 20, \
        "Press CATALOG for a list of apps.\n"+\
        "\n"+\
        "Go to https://github.com/mgismissing/deimos for\n"+\
        "more info about this project.", \
    SCR.ENUM.PIXEL.BLACK, font.miniwi))

applist = [,]
for app -> apps
    module = app[1]
    {applist}<-("[LIB] " if hasattr(module, "lib") else "")..module.name
.

def protected_run(func)
    try
        <-func!
    .catch loader.Error;
    .catch Exception = e
        ui.notify("Python error", (e?.__name__)+":\n"+(e as str), res.ui.error, wait=-)
        raise e
    .
.

def run_app(app)
    def run; loader.run(app);.
    protected_run(run)
.

try
    while True
        screen.clear!
        scr.render!
        screen.apply!
        key = keyboard.get_next!
        if key == KB.KEYS.SETTINGS
            c = choose(["Update from OFW repo", "WiFi", "Power", "Debug",])
            if c == 0
                if wifi.support
                    if wifi.choose_connect!
                        ui.notify(
                            "Updating",
                            "OS will be restarted automatically after the\nupdate.",
                            res.ui.info,
                            wait=-
                        )
                        protected_run(update.download)
                        machine.reset!
                    .else
                        ui.notify(
                            "No connection",
                            "A stable Internet connection is required to\nperform updates.",
                            res.ui.error
                        )
                    .
                .else
                    ui.notify(
                        "No WiFi support",
                        "WiFi support is required to perform updates.",
                        res.ui.error
                    )
                .
            .elif c == 1
                if wifi.support
                    wifi.choose_connect!
                .else
                    ui.notify(
                        "No WiFi support",
                        "WiFi support is required to connect to a\nnetwork.",
                        res.ui.error
                    )
                .
            .elif c == 2
                c = choose(["Reset environment", "Reboot",])
                if c == 0
                    machine.soft_reset!
                .elif c == 1
                    machine.reset!
                .
            .elif c == 3
                ui.notify(
                    "Debug mode",
                    "Control has been redirected to USB",
                    res.ui.info,
                    wait=-
                )
                raise DebugInterrupt("Returning to REPL")
            .
        .elif key == KB.KEYS.CATALOG
            c = choose(applist)
            if c >= 0
                run_app(apps[c][0])
            .
        .
    .
.catch DebugInterrupt = e
    <|"DEBUG: "..(e as str)
.