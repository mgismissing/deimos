use * from cwio
use * from cwio.const
use Font from cwio.font
use time
use machine
use Pin from machine
use sys
use os
use ui
use choose from ui
use res
use wifi
use wlan from wifi
use update
use garbage
use loader
use apps from loader

class DebugInterrupt from Exception;;.

init!

screen.on!
screen.init!
keyboard.init!

scr = ui.Screen!

scr \
    + (title    := ui.Label(0, 0, "Welcome to Deimos", SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)) \
    + (desc     := ui.Label(0, 20, \
        "Press HOME for a list of apps.\n"+\
        "\n"+\
        "Go to https://github.com/mgismissing/deimos for\n"+\
        "more info about this project.", \
    SCR.ENUM.PIXEL.BLACK, font.miniwi))

applist = [,]
for app -> apps
    module = app[1]
    {applist}<-("[LIB] " if hasattr(module, "lib") else "")..module.name
.

def protected_run(func)
    try
        <-func!
    .catch loader.Error;
    .catch Exception = e
        screen.clear!
        screen.write(e?.__name__, 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
        screen.write(e as str, 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
        screen.apply!
        raise e
    .
.

def run_app(app)
    def run; loader.run(app);.
    protected_run(run)
.

try
    while True
        screen.clear!
        scr.render!
        screen.apply!
        key = keyboard.get_next!
        if key == KB.KEYS.SETTINGS
            c = choose(["Update from OFW repo", "Power", "Debug"])
            if c == 0
                if wifi.support
                    if (not wlan.active!) or (not wlan.isconnected!); wifi.choose_connect!;.
                    if wlan.active! and wlan.isconnected!
                        screen.clear!
                        screen.write("Updating...", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                        screen.write("OS will be restarted automatically after the\nupdate.", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                        screen.apply!
                        protected_run(update.download)
                        machine.reset!
                    .else
                        screen.clear!
                        screen.write("Connection error", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                        screen.write("A stable Internet connection is required to\nperform updates.", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                        screen.apply!
                        keyboard.wait!
                    .
                .else
                    screen.clear!
                    screen.write("No WiFi support", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                    screen.write("WiFi support is required to perform updates.", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                    screen.apply!
                    keyboard.wait!
                .
            .elif c == 1
                c = choose(["Reset environment", "Reboot"])
                if c == 0
                    machine.soft_reset!
                .elif c == 1
                    machine.reset!
                .
            .elif c == 2
                screen.clear!
                screen.write("Debug mode", 0, 0, SCR.ENUM.PIXEL.BLACK, font.classwiz_cw)
                screen.write("Control has been redirected to USB", 0, 14, SCR.ENUM.PIXEL.BLACK, font.miniwi)
                screen.apply!
                raise DebugInterrupt("Returning to REPL")
            .
        .elif key == KB.KEYS.HOME
            c = choose(applist)
            if c >= 0
                run_app(apps[c][0])
            .
        .elif key == KB.KEYS.TOOLS
            ui.choose_file!
        .
    .
.catch DebugInterrupt = e
    <|"DEBUG: "..(e as str)
.