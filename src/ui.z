use screen from cwio

def choose(items: list(str)): int
    screen.clear!

    x = 0
    y = 0
    f = font.miniwi
    h = f!.h
    for item at i -> items
        screen.write(item, x+2, (y+(h+1)*i)+1, SCR.ENUM.PIXEL.BLACK, f)
    .
    oldsel = None
    sel = 0
    while True
        for item at i -> items
            if i == sel or i == oldsel
                screen.box(x, y+(h+1)*i, SCR.WIDTH, h+1, (SCR.ENUM.PIXEL.BLACK if i == sel else SCR.ENUM.PIXEL.WHITE))
            .
        .
        screen.apply!
        while keyboard.pressed(KEYBOARD.KEYS.OK);;.
        key = keyboard.get_next!
        if key == KEYBOARD.KEYS.UP
            oldsel = sel
            sel = ($-1) % #items
        .elif key == KEYBOARD.KEYS.DOWN
            oldsel = sel
            sel = ($+1) % #items
        .elif key == KEYBOARD.KEYS.OK
            <-sel
        .elif key == KEYBOARD.KEYS.BACK
            <- -1
        .
    .
.

class Widget
    /* @x, @y;;.

    /render ox: int, oy: int;;.
    /update;;.
.

class Label from Widget
    /* ^x, ^y, @text, @color, @font;;.

    /render ox: int, oy: int
        screen.write(@.text, @.x + ox, @.y + oy, @.color, @.font)
    .
.

class Image from Widget
    /* ^x, ^y, @img, @color;;.

    /render ox: int, oy: int
        screen.image(@.img, @.x+ox, @.y+oy, @.color)
    .
.

class SizableWidget
    /* ^x, ^y, @w, @h;;.
.

class Container from SizableWidget
    /* ^x, ^y, ^w, ^h; @.children = [,];.

    /render ox: int, oy: int
        for w -> @.children
            w.__render__(@.x + ox, @.y + oy)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
    .
.

class Screen
    /*; @.children = [,];.

    def @render
        for w -> @.children
            w.__render__(0, 0)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
        <-@
    .

    /add w: Widget; @.add(w); <-@;.
.