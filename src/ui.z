use * from cwio
use os
use fs

def choose(items: list(str), msg: str | None = None): int
    refresh = True
    x = 0
    y = 0
    f = font.miniwi
    h = f!.h
    oldsel = 0
    sel = 0
    oldpage = None
    page = 0
    while True
        page = sel // 4
        curr = items[page*4:(page+1)*4]
        if oldpage != page
            oldpage = page
            refresh = True
        .
        if refresh
            refresh = False
            screen.clear!
            if msg; screen.write(msg, 0, 0, SCR.ENUM.PIXEL.BLACK, font.miniwi);.
            for item at i -> curr
                screen.write(item, x+2, (y+(h+2)*i)+1+12, SCR.ENUM.PIXEL.BLACK, f)
            .
        .
        for item at i -> curr
            if i == sel % 4 or i == oldsel % 4
                screen.box(x, y+(h+2)*i+12, SCR.WIDTH, h+2, (SCR.ENUM.PIXEL.BLACK if i == sel % 4 else SCR.ENUM.PIXEL.WHITE))
            .
        .
        screen.apply!
        while keyboard.pressed(KB.KEYS.OK);;.
        key = keyboard.get_next!
        oldsel = sel
        if key == KB.KEYS.UP
            sel = ($-1) % #items
        .elif key == KB.KEYS.DOWN
            sel = ($+1) % #items
        .elif key == KB.KEYS.OK
            <-sel
        .elif key == KB.KEYS.BACK
            <- -1
        .
    .
.

def choose_file(start: str = "/")
    curr = os.listdir(start)
    path = choose(curr, start)
    if path == -1
        <- -1
    .
    
    path = start..curr[$]
    if fs.isfile(path)
        <-path
    .elif fs.isdir(path)
        <-choose_file(path.."/")
    .

    <- -1
.

keypad = [
    "8" = ["a", "b", "c",],
    "9" = ["d", "e", "f",],
    "4" = ["g", "h", "i",],
    "5" = ["j", "k", "l",],
    "6" = ["m", "n", "o",],
    "1" = ["p", "q", "r", "s",],
    "2" = ["t", "u", "v",],
    "3" = ["w", "x", "y", "z",],
    "0" = [" ",]
]

def ask(msg: str | None = None)
    buf = ""
    shift = False
    while True
        screen.clear!
        if msg; screen.write(msg, 0, 0, SCR.ENUM.PIXEL.BLACK, font.miniwi);.
        screen.write(buf + "|", 0, (8 if msg else 0), SCR.ENUM.PIXEL.BLACK, font.miniwi)
        screen.apply!
        while keyboard.pressed_any!;;.
        key = keyboard.get_next!
        if          key == KB.KEYS.SHIFT; shift = not $;
        .elif       key == KB.KEYS.FORMAT and #buf
            last = buf[-1]
            if last.isdigit!
                count = 0
                for c -> buf as reversed
                    if c != last; break;.
                    count++
                .
                if last in keypad
                    letters = keypad[last]
                    if count <= #letters
                        letter = letters[count-1]
                        if shift
                            shift = False
                            letter .= upper!
                        .
                        buf = $[:-count] + letter
                    .
                .
            .
        .elif shift
            shift = False
        .else
            if      key == KB.KEYS.EXE    ; <-buf;
            .elif   key == KB.KEYS.BKSPACE; buf = $[:-1];
            .elif   key == KB.KEYS.N0     ; buf += "0";
            .elif   key == KB.KEYS.N1     ; buf += "1";
            .elif   key == KB.KEYS.N2     ; buf += "2";
            .elif   key == KB.KEYS.N3     ; buf += "3";
            .elif   key == KB.KEYS.N4     ; buf += "4";
            .elif   key == KB.KEYS.N5     ; buf += "5";
            .elif   key == KB.KEYS.N6     ; buf += "6";
            .elif   key == KB.KEYS.N7     ; buf += "7";
            .elif   key == KB.KEYS.N8     ; buf += "8";
            .elif   key == KB.KEYS.N9     ; buf += "9";
            .
        .
    .
.

class Widget
    /* @x, @y;;.

    /render ox: int, oy: int;;.
    /update;;.
.

class Label from Widget
    /* ^x, ^y, @text, @color, @font;;.

    /render ox: int, oy: int
        screen.write(@.text, @.x + ox, @.y + oy, @.color, @.font)
    .
.

class Image from Widget
    /* ^x, ^y, @img, @color;;.

    /render ox: int, oy: int
        screen.image(@.img, @.x+ox, @.y+oy, @.color)
    .
.

class SizableWidget
    /* ^x, ^y, @w, @h;;.
.

class Container from SizableWidget
    /* ^x, ^y, ^w, ^h; @.children = [,];.

    /render ox: int, oy: int
        for w -> @.children
            w.__render__(@.x + ox, @.y + oy)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
    .
.

class Screen
    /*; @.children = [,];.

    def @render
        for w -> @.children
            w.__render__(0, 0)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
        <-@
    .

    /add w: Widget; @.add(w); <-@;.
.