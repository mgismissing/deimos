use * from cwio
use os

def choose(items: list(str)): int
    screen.clear!

    x = 0
    y = 0
    f = font.miniwi
    h = f!.h
    for item at i -> items
        screen.write(item, x+2, (y+(h+2)*i)+1, SCR.ENUM.PIXEL.BLACK, f)
    .
    oldsel = None
    sel = 0
    while True
        for item at i -> items
            if i == sel or i == oldsel
                screen.box(x, y+(h+2)*i, SCR.WIDTH, h+2, (SCR.ENUM.PIXEL.BLACK if i == sel else SCR.ENUM.PIXEL.WHITE))
            .
        .
        screen.apply!
        while keyboard.pressed(KEYBOARD.KEYS.OK);;.
        key = keyboard.get_next!
        if key == KEYBOARD.KEYS.UP
            oldsel = sel
            sel = ($-1) % #items
        .elif key == KEYBOARD.KEYS.DOWN
            oldsel = sel
            sel = ($+1) % #items
        .elif key == KEYBOARD.KEYS.OK
            <-sel
        .elif key == KEYBOARD.KEYS.BACK
            <- -1
        .
    .
.

def choose_file(start: str = "/")
    
.

keypad = [
    "8" = ["a", "b", "c",],
    "9" = ["d", "e", "f",],
    "4" = ["g", "h", "i",],
    "5" = ["j", "k", "l",],
    "6" = ["m", "n", "o",],
    "1" = ["p", "q", "r", "s",],
    "2" = ["t", "u", "v",],
    "3" = ["w", "x", "y", "z",],
    "0" = [" ",]
]

def ask(msg: str)
    buf = ""
    shift = False
    while True
        screen.clear!
        screen.write(msg, 0, 0, SCR.ENUM.PIXEL.BLACK, font.miniwi)
        screen.write(buf + "|", 0, 8, SCR.ENUM.PIXEL.BLACK, font.miniwi)
        screen.apply!
        while keyboard.pressed_any!;;.
        key = keyboard.get_next!
        if          key == KEYBOARD.KEYS.SHIFT; shift = not $;
        .elif       key == KEYBOARD.KEYS.FORMAT and #buf
            last = buf[-1]
            if last.isdigit!
                count = 0
                for c -> buf as reversed
                    if c != last; break;.
                    count++
                .
                if last in keypad
                    letters = keypad[last]
                    if count <= #letters
                        letter = letters[count-1]
                        if shift
                            shift = False
                            letter .= upper!
                        .
                        buf = $[:-count] + letter
                    .
                .
            .
        .elif shift
            shift = False
        .else
            if      key == KEYBOARD.KEYS.EXE    ; <-buf;
            .elif   key == KEYBOARD.KEYS.BKSPACE; buf = $[:-1];
            .elif   key == KEYBOARD.KEYS.N0     ; buf += "0";
            .elif   key == KEYBOARD.KEYS.N1     ; buf += "1";
            .elif   key == KEYBOARD.KEYS.N2     ; buf += "2";
            .elif   key == KEYBOARD.KEYS.N3     ; buf += "3";
            .elif   key == KEYBOARD.KEYS.N4     ; buf += "4";
            .elif   key == KEYBOARD.KEYS.N5     ; buf += "5";
            .elif   key == KEYBOARD.KEYS.N6     ; buf += "6";
            .elif   key == KEYBOARD.KEYS.N7     ; buf += "7";
            .elif   key == KEYBOARD.KEYS.N8     ; buf += "8";
            .elif   key == KEYBOARD.KEYS.N9     ; buf += "9";
            .
        .
    .
.

class Widget
    /* @x, @y;;.

    /render ox: int, oy: int;;.
    /update;;.
.

class Label from Widget
    /* ^x, ^y, @text, @color, @font;;.

    /render ox: int, oy: int
        screen.write(@.text, @.x + ox, @.y + oy, @.color, @.font)
    .
.

class Image from Widget
    /* ^x, ^y, @img, @color;;.

    /render ox: int, oy: int
        screen.image(@.img, @.x+ox, @.y+oy, @.color)
    .
.

class SizableWidget
    /* ^x, ^y, @w, @h;;.
.

class Container from SizableWidget
    /* ^x, ^y, ^w, ^h; @.children = [,];.

    /render ox: int, oy: int
        for w -> @.children
            w.__render__(@.x + ox, @.y + oy)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
    .
.

class Screen
    /*; @.children = [,];.

    def @render
        for w -> @.children
            w.__render__(0, 0)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
        <-@
    .

    /add w: Widget; @.add(w); <-@;.
.