use * from cwio
use * from cwio.const
use MonoImage from cwio.screen
use os
use fs
use res

def notify(title: str, msg: str, icon: MonoImage | None = None, wait: bool = True)
    screen.clear!
    if icon
        screen.image(icon, 0, 0, SCR.COLOR.BLACK)
        screen.write(title, 26, 6, SCR.COLOR.BLACK, font.classwiz_cw)
    .else
        screen.write(title, 0, 6, SCR.COLOR.BLACK, font.classwiz_cw)
    .
    screen.write(msg, 0, 24, SCR.COLOR.BLACK, font.miniwi)
    screen.apply!
    if wait; keyboard.wait!;.
.

def choose(items: list(str), msg: str | None = None): int
    refresh = True
    x = 0
    y = 0
    f = font.miniwi
    h = f!.h
    oldsel = 0
    sel = 0
    oldpage = None
    page = 0
    while True
        page = sel // 4
        curr = items[page*4:(page+1)*4]
        if oldpage != page
            oldpage = page
            refresh = True
        .
        if refresh
            refresh = False
            screen.clear!
            if msg; screen.write(msg, 0, 0, SCR.COLOR.BLACK, font.miniwi);.
            for item at i -> curr
                ix = x+2
                iy = (y+(h+2)*i)+12
                screen.image(res.ui.circled(i+1), ix, iy, SCR.COLOR.BLACK)
                screen.write(item, ix+12, iy+1, SCR.COLOR.BLACK, f)
            .
        .
        for item at i -> curr
            if i == sel % 4 or i == oldsel % 4
                screen.box(x+12, y+(h+2)*i+12, SCR.WIDTH-12, h+2, (SCR.COLOR.BLACK if i == sel % 4 else SCR.COLOR.WHITE))
            .
        .
        screen.apply!
        while keyboard.pressed(KB.KEY.OK);;.
        key = keyboard.get_next!
        oldsel = sel
        if key == KB.KEY.UP
            sel = ($-1) % #items
        .elif key == KB.KEY.DOWN
            sel = ($+1) % #items
        .elif key == KB.KEY.PG_UP
            sel = ($-4) % #items
        .elif key == KB.KEY.PG_DOWN
            sel = ($+4) % #items
        .elif key == KB.KEY.OK
            <-sel
        .elif key == KB.KEY.BACK
            <- -1
        .elif key == KB.KEY.N1
            <-page*4
        .elif key == KB.KEY.N2
            <-page*4+1
        .elif key == KB.KEY.N3
            <-page*4+2
        .elif key == KB.KEY.N4
            <-page*4+3
        .
    .
.

def choose_file(start: str = "/")
    curr = os.listdir(start)
    path = choose(curr, start)
    if path == -1
        <- -1
    .
    
    path = start..curr[$]
    if fs.isfile(path)
        <-path
    .elif fs.isdir(path)
        <-choose_file(path.."/")
    .

    <- -1
.

keypad = [
    "8" = ["a", "b", "c",],
    "9" = ["d", "e", "f",],
    "4" = ["g", "h", "i",],
    "5" = ["j", "k", "l",],
    "6" = ["m", "n", "o",],
    "1" = ["p", "q", "r", "s",],
    "2" = ["t", "u", "v",],
    "3" = ["w", "x", "y", "z",],
    "0" = [" ",]
]

def ask(msg: str | None = None, default: str = "")
    buf = default
    shift = False
    while True
        screen.clear!
        if msg; screen.write(msg, 0, 0, SCR.COLOR.BLACK, font.miniwi);.
        screen.write(buf + "|", 0, (8 if msg else 0), SCR.COLOR.BLACK, font.miniwi)
        screen.apply!
        while keyboard.pressed_any!;;.
        key = keyboard.get_next!
        if          key == KB.KEY.SHIFT; shift = not $;
        .elif       key == KB.KEY.FORMAT and #buf
            last = buf[-1]
            if last.isdigit!
                count = 0
                for c -> buf as reversed
                    if c != last; break;.
                    count++
                .
                if last in keypad
                    letters = keypad[last]
                    if count <= #letters
                        letter = letters[count-1]
                        if shift
                            shift = False
                            letter .= upper!
                        .
                        buf = $[:-count] + letter
                    .
                .
            .
        .elif shift
            shift = False
        .else
            if      key == KB.KEY.EXE    ; <-buf;
            .elif   key == KB.KEY.BKSPACE; buf = $[:-1];
            .elif   key == KB.KEY.N0     ; buf += "0";
            .elif   key == KB.KEY.N1     ; buf += "1";
            .elif   key == KB.KEY.N2     ; buf += "2";
            .elif   key == KB.KEY.N3     ; buf += "3";
            .elif   key == KB.KEY.N4     ; buf += "4";
            .elif   key == KB.KEY.N5     ; buf += "5";
            .elif   key == KB.KEY.N6     ; buf += "6";
            .elif   key == KB.KEY.N7     ; buf += "7";
            .elif   key == KB.KEY.N8     ; buf += "8";
            .elif   key == KB.KEY.N9     ; buf += "9";
            .
        .
    .
.

class Widget
    /* @x, @y;;.

    /render ox: int, oy: int;;.
    /update;;.
.

class Label from Widget
    /* ^x, ^y, @text, @color, @font;;.

    /render ox: int, oy: int
        screen.write(@.text, @.x + ox, @.y + oy, @.color, @.font)
    .
.

class Image from Widget
    /* ^x, ^y, @img, @color;;.

    /render ox: int, oy: int
        screen.image(@.img, @.x+ox, @.y+oy, @.color)
    .
.

class SizableWidget
    /* ^x, ^y, @w, @h;;.
.

class Container from SizableWidget
    /* ^x, ^y, ^w, ^h; @.children = [,];.

    /render ox: int, oy: int
        for w -> @.children
            w.__render__(@.x + ox, @.y + oy)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
    .
.

class Screen
    /*; @.children = [,];.

    def @render
        for w -> @.children
            w.__render__(0, 0)
        .
    .

    def @add(w: Widget): None
        {@.children}<-w
        <-@
    .

    /add w: Widget; @.add(w); <-@;.
.